description = "gRPC: OkHttp"

dependencies {
    compile project(':grpc-core'),
            libraries.okio

    compile (libraries.okhttp) {
        // prefer 1.13.0 from libraries instead of 1.6.0
        exclude group: 'com.squareup.okio', module: 'okio'
    }

    // Tests depend on base class defined by core module.
    testCompile project(':grpc-core').sourceSets.test.output,
            project(':grpc-testing'),
            project(':grpc-netty')
    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    signature "net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature"
}

project.sourceSets {
    main { java { srcDir "${projectDir}/third_party/okhttp/main/java" } }
    test { java { srcDir "${projectDir}/third_party/okhttp/test/java" } }
}

checkstyleMain.exclude '**/io/grpc/okhttp/internal/**'

javadoc.exclude 'io/grpc/okhttp/internal/**'
javadoc.options.links 'http://square.github.io/okhttp/2.x/okhttp/'
