plugins {
    id "java-library"
    id "maven-publish"

    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: OpenTelemetry'

dependencies {
    api project(':grpc-api')
    implementation libraries.guava,
            project(':grpc-core'),
            libraries.opentelemetry.api,
            libraries.auto.value.annotations

    testImplementation project(':grpc-testing'),
            project(':grpc-inprocess'),
            testFixtures(project(':grpc-core')),
            testFixtures(project(':grpc-api')),
            libraries.opentelemetry.sdk.testing,
            libraries.assertj.core // opentelemetry.sdk.testing uses compileOnly for assertj

    annotationProcessor libraries.auto.value

    signature (libraries.signature.java) {
        artifact {
            extension = "signature"
        }
    }
}

tasks.named("jar").configure {
    manifest {
        attributes('Automatic-Module-Name': 'io.grpc.opentelemetry')
    }
}

tasks.named("compileJava").configure {
    it.options.compilerArgs += [
            // only has AutoValue annotation processor
            "-Xlint:-processing"
    ]
    appendToProperty(
            it.options.errorprone.excludedPaths,
            ".*/build/generated/sources/annotationProcessor/java/.*",
            "|")
}

tasks.named("javadoc").configure {
    exclude 'io/grpc/opentelemetry/internal/**'
}
