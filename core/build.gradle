plugins {
    id "java"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: Core'

evaluationDependsOn(project(':grpc-context').path)
evaluationDependsOn(project(':grpc-api').path)

dependencies {
    compile project(':grpc-api'),
            libraries.gson,
            libraries.android_annotations,
            libraries.errorprone // prefer our version to perfmark's 2.3.3
    compile (libraries.perfmark) {
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }

    testCompile project(':grpc-context').sourceSets.test.output,
            project(':grpc-api').sourceSets.test.output,
            project(':grpc-testing'),
            project(':grpc-grpclb'),
            libraries.guava_testlib
    
    testRuntimeOnly project(':grpc-census')

    jmh project(':grpc-testing')

    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    signature "net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature"
}

javadoc {
    exclude 'io/grpc/internal/**'
    // Disabled until kinda stable.
    exclude 'io/grpc/perfmark/**'
}

animalsniffer {
    // Don't check sourceSets.jmh
    sourceSets = [
        sourceSets.main,
        sourceSets.test
    ]
}

import net.ltgt.gradle.errorprone.CheckSeverity

plugins.withId("java") {
    compileJmhJava {
        // This project targets Java 7 (no method references)
        options.errorprone.check("UnnecessaryAnonymousClass", CheckSeverity.OFF)
    }
}
