plugins {
    id "java"
    id "maven-publish"
}

description = 'gRPC: Census'

evaluationDependsOn(project(':grpc-api').path)

dependencies {
    compile project(':grpc-api')
            
    compile (libraries.opencensus_api) {
        // prefer 3.0.2 from libraries instead of 3.0.1
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // prefer 20.0 from libraries instead of 19.0
        exclude group: 'com.google.guava', module: 'guava'
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
    }
    
    compile (libraries.opencensus_contrib_grpc_metrics) {
        // prefer 3.0.2 from libraries instead of 3.0.1
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
        // prefer 20.0 from libraries instead of 19.0
        exclude group: 'com.google.guava', module: 'guava'
    }

    testCompile project(':grpc-api').sourceSets.test.output,
            project(':grpc-context').sourceSets.test.output,
            project(':grpc-core').sourceSets.test.output,
            project(':grpc-testing'),
            libraries.guava_testlib,
            libraries.opencensus_impl
}

javadoc {
    failOnError false  // no public or protected classes found to document
    exclude 'io/grpc/census/internal/**'
    exclude 'io/grpc/census/Internal*'
}
