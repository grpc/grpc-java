plugins {
    id "java-library"
    id "maven-publish"

    id "ru.vyarus.animalsniffer"
}

description = 'gRPC: Census'

tasks.named("jar").configure {
    manifest {
        attributes('Automatic-Module-Name': 'io.grpc.census')
    }
}

dependencies {
    api project(':grpc-api')
    // force dependent jars to depend on latest grpc-context
    runtimeOnly project(":grpc-context")
    implementation libraries.guava,
            project(":grpc-context"), // Override opencensus dependency with our newer version
            libraries.opencensus.api,
            libraries.opencensus.contrib.grpc.metrics

    testImplementation testFixtures(project(':grpc-api')),
            testFixtures(project(':grpc-core')),
            project(':grpc-testing'),
            libraries.opencensus.impl

    signature (libraries.signature.java) {
        artifact {
            extension = "signature"
        }
    }
    signature (libraries.signature.android) {
        artifact {
            extension = "signature"
        }
    }
}

tasks.named("javadoc").configure {
    failOnError false  // no public or protected classes found to document
    exclude 'io/grpc/census/internal/**'
    exclude 'io/grpc/census/Internal*'
}
