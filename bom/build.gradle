plugins {
  id 'java-platform'
  id "maven-publish"
}

description = 'gRPC: BOM'

gradle.projectsEvaluated {
  def projectsToInclude = rootProject.subprojects.findAll {
    it.plugins.hasPlugin('java')
        && it.plugins.hasPlugin('maven-publish')
        && it.tasks.findByName('publishMavenPublicationToMavenRepository')?.enabled
  }
  dependencies {
    constraints {
      projectsToInclude.each { api it }
      // compiler. should have "<type>pom</type>"
      // not sure how to express in gradle, patched in XML
      api "$project.group:protoc-gen-grpc-java:$project.version"
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.javaPlatform
      pom.withXml {
        def lastDependency = asNode().dependencyManagement.dependencies.dependency.last()
        assert lastDependency.artifactId.text() == 'protoc-gen-grpc-java'
        lastDependency.appendNode('type', 'pom')
      }
    }
  }
}
