plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.japicmp"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: Stub"

tasks.named("jar").configure {
    manifest {
        attributes('Automatic-Module-Name': 'io.grpc.stub')
    }
}

dependencies {
    api project(':grpc-api'),
        libraries.animalsniffer.annotations,
        libraries.guava
    implementation libraries.errorprone.annotations
    testImplementation libraries.truth,
            project(':grpc-inprocess'),
            project(':grpc-testing'),
            testFixtures(project(':grpc-api'))
    signature (libraries.signature.java) {
        artifact {
            extension = "signature"
        }
    }
    signature (libraries.signature.android) {
        artifact {
            extension = "signature"
        }
    }
}

tasks.named("javadoc").configure {
    exclude 'io/grpc/stub/Internal*'
}
